plugins {
    id 'java'
    id "maven-publish"
    id "io.github.goooler.shadow" version "8.1.7"
    id "xyz.jpenilla.run-paper" version "2.3.0"
}

group = 'com.starseekstudios'
version = System.getenv("VERSION") ?: '0.0.0-dev'

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        minecraftVersion("1.20.6")
    }
}

shadowJar {
    relocate 'com.jeff_media.morepersistentdatatypes', 'com.starseekstudios.mysticore.morepersistentdatatypes'
    relocate 'de.exlll.configlib', 'com.starseekstudios.mysticore.configlib'
    relocate 'dev.jorel.commandapi', 'com.starseekstudios.mysticore.commandapi'
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "NBT-API"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "persistent-data-types-repo"
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }

}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    implementation 'com.jeff_media:MorePersistentDataTypes:2.4.0'
    implementation "de.exlll:configlib-paper:4.5.0"
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.3"
    compileOnly "dev.jorel:commandapi-annotations:9.5.3"
    annotationProcessor "dev.jorel:commandapi-annotations:9.5.3"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Mystievous/MystiCore")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId 'com.starseekstudios'
            artifactId 'mysticore'
            from components.java
        }
    }
}